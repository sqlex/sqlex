plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
    id 'signing'
}

gradlePlugin {
    plugins {
        sqlex {
            id = "me.danwi.sqlex"
            implementationClass = "me.danwi.sqlex.gradle.SqlExPlugin"
        }
    }
}

afterEvaluate {
    publishing.publications.forEach {
        it.pom {
            name = project.name
            description = "sqlex ${project.name} component"
            url = 'https://github.com/sqlex'
            scm {
                connection = 'scm:git:https://github.com/sqlex/sqlex.git'
                developerConnection = 'scm:git:https://github.com/sqlex/sqlex.git'
                url = 'https://github.com/sqlex'
            }
            licenses {
                license {
                    name = 'The Apache License, Version 2.0'
                    url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id = 'sqlex'
                    name = 'sqlex'
                    email = 'demon@danwi.me'
                }
            }
        }
    }

    if (localProperties.get("signing.key") != null)
        signing {
            useInMemoryPgpKeys(localProperties.get("signing.key"), localProperties.get("signing.password"))
            publishing.publications.forEach{ sign it }
        }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation project(":parser")
}

//kotlin和groovy兼容交互
compileGroovy.dependsOn compileKotlin
compileGroovy.classpath += files(compileKotlin.destinationDir)
classes.dependsOn compileGroovy